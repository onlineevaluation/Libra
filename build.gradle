import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
    ext {
        kotlinVersion = '1.3.10'
        springBootVersion = '2.1.1.RELEASE'
        dokka_version = '0.9.17'
    }
    repositories {
        jcenter()
        mavenCentral()

    }
    dependencies {

        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.10"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.10"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'jacoco'

group = 'com.nuc'
version = '2.0.0.' + getDate()
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

test {
    testLogging {
        // set options for log level LIFECYCLE
        events "failed"
        exceptionFormat "short"

        // set options for log level DEBUG
        debug {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }
}

dependencies {
    // jpa
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-jdbc')
    // web
    implementation('org.springframework.boot:spring-boot-starter-web')
    // kotlin
    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    // mysql链接
    runtimeOnly('mysql:mysql-connector-java')
    // swagger 2
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.8.0'

//    // security
    implementation('org.springframework.boot:spring-boot-starter-security')
//    compile group: 'org.apache.shiro', name: 'shiro-spring', version: '1.4.0'

    //jwt
    implementation 'io.jsonwebtoken:jjwt:0.9.0'
    implementation 'com.auth0:java-jwt:3.5.0'
    // fastjson
    implementation('com.alibaba:fastjson:1.2.46')
    // mq
    implementation('org.springframework.boot:spring-boot-starter-amqp')
    // word 分词，词频
    implementation group: 'org.apdplat', name: 'word', version: '1.3'
    // sentry 日志监控
    implementation 'io.sentry:sentry-spring:1.6.8'
    implementation group: 'com.getsentry.raven', name: 'raven-logback', version: '8.0.3'
    // Kotlin-Reflect-Tools-For-JVM
    implementation 'wu.seal:kotlin-reflect-tools-for-jvm:1.1.2'
    // 缓存
//    compile 'org.springframework.boot:spring-boot-starter-redis'
    // test
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'

}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format("yyMMddHH")
    return formattedDate
}

task codeCoverageReport(type: JacocoReport) {
    classDirectories = files('build/classes')
    sourceDirectories = files('src/main/kotlin')

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

check.dependsOn codeCoverageReport

//task ci(type: BootRun) {
//    classpath = sourceSets.main.runtimeClasspath
//    main = 'com.nuc.libra.EvaluateApplication'
//    systemProperty 'spring.profiles.active', 'ci'
//}
